ext {
  kotlinVersion = '1.0.5'

  compileSdkVersion = 25
  buildToolsVersion = '25.0.1'

  dep = [
      androidPlugin: 'com.android.tools.build:gradle:2.3.0-beta1',
      supportAnnotations: 'com.android.support:support-annotations:25.1.0',
      compiletesting: 'com.google.testing.compile:compile-testing:0.10',
      javaPoet: 'com.squareup:javapoet:1.8.0',
      moshi: 'com.squareup.moshi:moshi:1.3.1',
      rxjava: 'io.reactivex:rxjava:1.2.3',
      jsr305: 'com.google.code.findbugs:jsr305:3.0.1',
      pluralizer: 'com.github.cesarferreira:kotlin-pluralizer:0.2.7',
      appcompat: 'com.android.support:appcompat-v7:25.0.1',
      retrofit: 'com.squareup.retrofit2:retrofit:2.1.0',
      retrofitMoshiConverter: 'com.squareup.retrofit2:converter-moshi:2.1.0',
      kotlinStdLib: "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion",
      kotlinReflect: "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion",
      kotlinGradlePlugin: "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion",
      gradleNodePlugin: "com.moowork.gradle:gradle-node-plugin:1.0.0",
      junit: 'junit:junit:4.12',
      spock: 'org.spockframework:spock-core:0.7-groovy-2.0',
      truth: 'com.google.truth:truth:0.30',
      mockWebServer: 'com.squareup.okhttp3:mockwebserver:3.5.0'
  ]

  isCi = "true" == System.getenv('CI')
}

subprojects {
  buildscript {
    repositories {
      mavenCentral()
      jcenter()
    }

    dependencies {
      classpath dep.kotlinGradlePlugin
    }
  }
  repositories {
    mavenCentral()
    jcenter()
    maven { url "https://jitpack.io" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
  }

  group = GROUP
  version = VERSION_NAME

  if (!project.name.equals('apollo-gradle-plugin')) {
    apply plugin: 'checkstyle'

    checkstyle {
      configFile rootProject.file('checkstyle.xml')
      configProperties = ['checkstyle.cache.file': rootProject.file('build/checkstyle.cache')]
      ignoreFailures false
      showViolations true
    }

    task checkstyle(type: Checkstyle) {
      source 'src/main/java'
      include '**/*.java'

      classpath = files()
    }

    afterEvaluate {
      if (project.tasks.findByName('check')) {
        check.dependsOn('checkstyle')
      }
    }
  }
}
